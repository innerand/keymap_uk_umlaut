#Maintainer: Andreas Innerlohninger <innerand at nxa dot at>
pkgname=xkeyboard-config-umlauts
pkgver=2.37
pkgrel=1
pkgdesc="X keyboard configuration files, gb layout with umlauts"
arch=(any)
license=('custom')
url="https://www.freedesktop.org/wiki/Software/XKeyboardConfig"
makedepends=('xorg-xkbcomp' 'libxslt' 'python' 'meson' 'git')
provides=('xkbdata' 'xkeyboard-config')
replaces=('xkbdata' 'xkeyboard-config')
conflicts=('xkbdata' 'xkeyboard-config')
# https://gitlab.freedesktop.org/xkeyboard-config/xkeyboard-config
source=(https://xorg.freedesktop.org/archive/individual/data/xkeyboard-config/xkeyboard-config-${pkgver}.tar.xz{,.sig})
validpgpkeys=('FFB4CCD275AAA422F5F9808E0661D98FC933A145') # Sergey Udaltsov <sergey.udaltsov@gmail.com>
sha256sums=('eb1383a5ac4b6210d7c7302b9d6fab052abdf51c5d2c9b55f1f779997ba68c6c'
            'SKIP')

prepare() {
  cd xkeyboard-config-${pkgver}
  patch -p1 -i ../../uk_umlaut.patch
}

build() {
  arch-meson xkeyboard-config-${pkgver} build \
	-D xkb-base="/usr/share/X11/xkb" \
	-D compat-rules=true \
	-D xorg-rules-symlinks=true

  # Print config
  meson configure build

  ninja -C build

}

package() {

  DESTDIR="$pkgdir" ninja -C build install

  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/xkeyboard-config"
  install -m644 xkeyboard-config-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/xkeyboard-config"
}
